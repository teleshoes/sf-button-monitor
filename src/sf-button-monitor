#!/usr/bin/perl
use strict;
use warnings;
use IO::Select;
use Time::HiRes qw(time);

my $INPUT_DEV_GLOB = "/dev/input/by-path/*gpio_keys*";
my $CONFIG_FILE = "$ENV{HOME}/.config/sf-button-monitor.conf";
my $LAST_WINDOW_TITLE_FILE = "/tmp/lipstick-window-title";

my $DEFAULT_MODEL = "F1521";
my $DEVICE_BYTE_OFFSETS = {
  #MODEL => [EVENT_BYTE_SIZE, BTN_ID_BYTE_INDEX, BTN_VAL_BYTE_INDEX]
  $DEFAULT_MODEL => [32, 10, 12],
  "H8314"        => [48, 18, 20],
};

my $DEFAULT_CONFIG_CONTENTS = ''
  . "action=torch,vd vd,screenLocked\n"
  ;

my $PTRN_MAX_MILLIS = 350;
my $PTRN_ACCIDENTAL_DOUBLE_MILLIS = 10;

my $PTRN_MATCH_ANYWHERE = "anywhere";
my $PTRN_MATCH_END_ONLY = "end-only";

sub getDeviceByteOffsets();
sub readButtonEvent($$$$);
sub trimPattern(@);
sub removeAccidentalDoubleClicks(@);
sub parseConfigFileActions($);
sub sortActionsLongestFirst(@);
sub formatPattern(@);
sub condensePattern(@);
sub parsePatternString($);
sub checkAction($$@);
sub checkCondition($);
sub getActionCommand($);
sub isScreenLocked();
sub allEq(@);
sub dbusSendCmd($$$@);
sub getActiveWindowCmd();
sub getActiveWindowPid();
sub isWindowActive($);
sub isAnyWindowActive();
sub nowMillis();

my $usage = "Usage:
  Perform actions when Sailfish OS hardware buttons are pressed.

  Supports volume up/down and camera half/full keys,
    when screen is locked or unlocked.

  Can run arbitrary shell commands, or a few built-in commands.

  Works best with automatic display-on disabled (requires mcetool).
    Install mcetool and run the following,
    to prevent unlocking when camera/volume is pressed:
      mcetool --set-exception-length-volume=0
      mcetool --set-exception-length-camera=0

  $0 -h | --help
    display this message

  $0
    -listen to $INPUT_DEV_GLOB
    -parse bytes into button press/release events
    -split sequences of events into patterns
    -read actions and patterns from $CONFIG_FILE
    -perform actions that match event patterns and system conditions


  actions are recorded in $CONFIG_FILE
    one per line, formatted like:
    action=ACTION,PTRN,CONDITION

  ACTION    = cmd(<CMD>) | repeat(<REPEAT_MS>,<CMD>) | torch | screenshot
              | reboot | shutdown | openCamera | selfie
              | newAlarm | newNote | writeEmail
                action to perform
                  cmd:         run shell command <CMD>
                  repeat:      run shell command <CMD> every <REPEAT_MILLIS>ms,
                               until another button <EVENT> happens
                               (if pattern ends in <BTN>-press, <BTN>-release will end it)
                  torch:       toggle flashlight
                  screenshot:  take a screenshot in ~/screenshot-<MILLIS>.png
                  reboot:      reboot phone
                  shutdown:    shutdown phone
                  openCamera:  open the camera app (in previous mode)
                  selfie:      open the camera app in selfie mode
                  newAlarm:    open app to add alarm
                  newNote:     open app to add note
                  writeEmail:  open app to write email
  CMD       = <any-string>
                any shell command: EXEC ARG ARG...
  REPEAT_MS = milliseconds to wait for <EVENT>s before re-running command
  CONDITION = always | screenLocked | screenUnlocked
              | app(CMD_REGEX) | home | noapp | anyapp | android
              | window(TITLE_REGEX)
                when to allow the command to run
                  always:         always
                  screenLocked:   when display is off and screen is locked
                  screenUnlocked: when display is on and screen is unlocked
                  app:            when cmd of topmost window process matches CMD_REGEX
                  home:           when screenUnlocked and there is no topmost window
                  noapp:          when there is no topmost window
                                    same as: 'home' or 'screenLocked'
                  anyapp:         when there is any topmost window
                                    same as: 'app()'
                  android:        when topmost window is aliendalvik
                                    same as: 'app(^system_server\$)'
                  window:         when screen is unlocked,
                                    and contents of $LAST_WINDOW_TITLE_FILE match TITLE_REGEX
                                    WARNING:
                                      that file is not set by this script
                                      you must manually alter lipstick to make window()
                                      do anything interesting at all
  CMD_REGEX = <any-string>
                regular expression used to match process cmd returned by ps

  BTN_PTRN  = <EVENT> | <EVENT> <BTN_PTRN>
                a full pattern string containing button press/releases
  EVENT     = <PRESS> | <RELEASE> | <CLICK> | <GROUP>
                a button press and/or release
  PRESS     = <BTN>-press
                pressing button named <BTN>
  RELEASE   = <BTN>-release
                releasing button named <BTN>
  CLICK     = <BTN>
                same as: <BTN>-press <BTN>-release
  GROUP     = <BTN>() | <BTN>( <BTN_PTRN> )
                same as: <BTN>-press <BTN_PTRN> <BTN>-release
  BTN       = vu | vd | ch | cf
                the name of a physical hardware button
                  vu: volume up
                  vd: volume down
                  ch: camera half
                  cf: camera full

  BTN_PTRN is a sequence of button presses/releases, with optional short synonyms
  e.g.:
     vd-press vd-release =>
         vd-press vd-release
     vd =>
         vd-press vd-release
     vd() =>
         vd-press vd-release
     vd vd =>
         vd-press vd-release vd-press vd-release
     vd(vu) =>
         vd-press vu-press vu-release vd-release
     ch(vd vd(vu vu()) cf) =>
         ch-press vd-press vd-release vd-press
         vu-press vu-release vu-press vu-release
         vd-release cf-press cf-release ch-release
";

my @BUTTONS = (
  [qw(115 VOL_UP   vu)],
  [qw(114 VOL_DOWN vd)],
  [qw(16  CAM_HALF ch)],
  [qw(254 CAM_FULL cf)],
);
my %OK_BUTTON_IDS = map {$$_[0] => 1} @BUTTONS;
my %BTN_NAMES = map {$$_[0] => $$_[1]} @BUTTONS;
my %BTN_SHORTNAMES = map {$$_[0] => $$_[2]} @BUTTONS;

my $TYPE_PRESS = "press";
my $TYPE_RELEASE = "release";

my %DBUS_ACTIONS = (
  torch      => dbusSendCmd('com.jolla.settings.system.flashlight', '/<DEST_OBJ>',
                  '<DEST>.toggleFlashlight'),
  screenshot => dbusSendCmd('org.nemomobile.lipstick', '/<DEST_OBJ>/screenshot',
                  '<DEST>.saveScreenshot', 'string:/home/nemo/screenshot-<MILLIS>.png'),
  reboot     => dbusSendCmd('com.nokia.dsme', '/<DEST_OBJ>/request',
                  '<DEST>.request.req_reboot'),
  shutdown   => dbusSendCmd('com.nokia.dsme', '/<DEST_OBJ>/request',
                  '<DEST>.request.req_shutdown'),
  openCamera => dbusSendCmd('com.jolla.camera', '/', '<DEST>.ui.showViewfinder'),
  selfie     => dbusSendCmd('com.jolla.camera', '/', '<DEST>.ui.showFrontViewfinder'),
  newAlarm   => dbusSendCmd('com.jolla.clock', '/', '<DEST>.newAlarm'),
  newNote    => dbusSendCmd('com.jolla.notes', '/', '<DEST>.newNote'),
  writeEmail => dbusSendCmd('com.jolla.email.ui', '/<DEST_OBJ>', '<DEST>.mailto'),
);

sub main(@){
  if(@_ == 1 and $_[0] =~ /^(-h|--help)$/){
    print $usage;
    exit 0;
  }elsif(@_ > 0){
    die $usage;
  }

  my $inputDev;
  my @inputDevs = glob($INPUT_DEV_GLOB);
  if(@inputDevs >= 1 and -e $inputDevs[0]){
    $inputDev = $inputDevs[0];
  }else{
    die "ERROR: could not find $INPUT_DEV_GLOB\n";
  }

  my ($eventByteSize, $buttonIdByteIndex, $buttonValByteIndex) = getDeviceByteOffsets();

  #auto-flush stdout
  select(STDOUT);
  $| = 1;

  my @actions = parseConfigFileActions($CONFIG_FILE);
  @actions = sortActionsLongestFirst @actions;

  my $inputDevFH;
  my @pattern;
  my $lastButtonMillis = nowMillis();
  open $inputDevFH, "< $inputDev";

  my $select = IO::Select->new();
  $select->add($inputDevFH);

  while(1){
    my ($buttonId, $value) = readButtonEvent(
      $select,
      $eventByteSize,
      $buttonIdByteIndex,
      $buttonValByteIndex
    );

    my $now = nowMillis();
    my $elapsedMillis = $now - $lastButtonMillis;
    $lastButtonMillis = $now;

    my $type = $value ? $TYPE_PRESS : $TYPE_RELEASE;

    my $event = {
      buttonId      => $buttonId,
      type          => $type,
      elapsedMillis => $elapsedMillis,
    };

    push @pattern, $event;
    @pattern = trimPattern @pattern;

    printf "EVENT: %8s => %-7s  %14dms  (%s)\n",
      $BTN_NAMES{$$event{buttonId}},
      $$event{type},
      $$event{elapsedMillis},
      formatPattern(@pattern),
      ;

    @pattern = removeAccidentalDoubleClicks(@pattern);

    for my $action(@actions){
      my $patternMatches = checkAction($PTRN_MATCH_ANYWHERE, $action, @pattern);
      if($patternMatches and checkCondition($$action{condition})){
        if(not $select->can_read($PTRN_MAX_MILLIS/1000.0)){
          my $command = getActionCommand $$action{action};
          print "RUNNING $command\n";
          system "$command &";

          if($$action{action} =~ /^repeat\((\d+)/){
            my $repeatMillis = $1;
            my $noButtonsSince = checkAction($PTRN_MATCH_END_ONLY, $action, @pattern);
            while($noButtonsSince){
              #wait repeatMillis ms for any more button presses/releases
              if(not $select->can_read($repeatMillis/1000.0)){
                print "RE-RUNNING $command\n";
                system "$command &";
              }else{
                $noButtonsSince = 0;
              }
            }
          }

          @pattern = ();
        }
      }
    }
  }
}

sub getDeviceByteOffsets(){
  my $model = `ssu mo 2>/dev/null`;
  chomp $model;
  $model =~ s/^Device model is: //;
  $model = uc $model;
  if(defined $$DEVICE_BYTE_OFFSETS{$model}){
    return @{$$DEVICE_BYTE_OFFSETS{$model}};
  }else{
    return @{$$DEVICE_BYTE_OFFSETS{$DEFAULT_MODEL}};
  }
}

#blocks until a valid button press/release event and then returns it
sub readButtonEvent($$$$){
  my ($select, $eventByteSize, $buttonIdByteIndex, $buttonValByteIndex) = @_;
  my $byteStr;
  while(1){
    my @readyFHs = $select->can_read(); #block forever
    if(@readyFHs > 0){
      my $inputDevFH = $readyFHs[0];
      read $inputDevFH, $byteStr, $eventByteSize;
      my @bytesDec = map {ord $_} split //, $byteStr;
      my $buttonId = $bytesDec[$buttonIdByteIndex];
      my $value = $bytesDec[$buttonValByteIndex];
      if(defined $OK_BUTTON_IDS{$buttonId}){
        return ($buttonId, $value);
      }
    }
  }
}

sub trimPattern(@){
  my (@pattern) = @_;
  my %states = map {$_ => 0} sort keys %OK_BUTTON_IDS;
  my $trimIndex = 0;
  for(my $i=0; $i<@pattern; $i++){
    my $event = $pattern[$i];
    my $btnId = $$event{buttonId};
    my @keysDown = grep {$states{$_}} sort keys %states;
    if(@keysDown == 0 and $$event{elapsedMillis} > $PTRN_MAX_MILLIS){
      $trimIndex = $i;
    }
    if($$event{type} eq $TYPE_PRESS){
      if($states{$btnId} == 1){
        print "    WARNING: press without release for $BTN_NAMES{$btnId}\n";
        $trimIndex = $i+1;
      }
      $states{$$event{buttonId}} = 1;
    }elsif($$event{type} eq $TYPE_RELEASE){
      if($states{$btnId} == 0){
        print "    WARNING: release without press for $BTN_NAMES{$btnId}\n";
        $trimIndex = $i+1;
      }
      $states{$$event{buttonId}} = 0;
    }
  }

  if($trimIndex >= @pattern){
    @pattern = ();
  }elsif($trimIndex > 0){
    @pattern = @pattern[$trimIndex..$#pattern];
  }

  return @pattern;
}

sub removeAccidentalDoubleClicks(@){
  my @pattern = @_;
  my %dupeIndexes;
  for(my $i=0; $i<@pattern; $i++){
    last if $i > @pattern - 3;
    my $initPress = $pattern[$i];
    my $initRelease = $pattern[$i+1];
    my $dupePress = $pattern[$i+2];

    next if $$initPress{type} ne $TYPE_PRESS;
    next if $$initRelease{type} ne $TYPE_RELEASE;
    next if $$dupePress{type} ne $TYPE_PRESS;

    my @events = ($initPress, $initRelease, $dupePress);
    my @buttonIds = map {$$_{buttonId}} @events;
    next if not allEq(@buttonIds);

    next if $$initRelease{elapsedMillis} > $PTRN_ACCIDENTAL_DOUBLE_MILLIS;
    next if $$dupePress{elapsedMillis} > $PTRN_ACCIDENTAL_DOUBLE_MILLIS;

    $dupeIndexes{$i+1} = 1; #skip the initial release
    $dupeIndexes{$i+2} = 1; #skip the extra press
  }

  my $dupeIndexCount = keys %dupeIndexes;
  if($dupeIndexCount > 0){
    my @newPattern;
    for(my $i=0; $i<@pattern; $i++){
      if(not defined $dupeIndexes{$i}){
        push @newPattern, $pattern[$i];
      }
    }
    printf "    FALSE DBL CLICK: %s => %s\n",
      formatPattern(@pattern),
      formatPattern(@newPattern),
      ;
    @pattern = @newPattern;
  }

  return @pattern;
}

sub parseConfigFileActions($){
  my ($file) = @_;
  my $contents;
  if(not -f $file){
    $contents = $DEFAULT_CONFIG_CONTENTS;
  }else{
    open FH, "< $file" or die "could not read $file\n$!\n";
    $contents = join '', <FH>;
    close FH;
  }

  my $dbusActionRe = join '|', sort keys %DBUS_ACTIONS;

  my $actionRe = "(?:cmd\\(.*\\)|repeat\\(\\d+,.*\\)|$dbusActionRe)";

  my @actions;

  my $condRe = join "|", (
    "always", "screenLocked", "screenUnlocked",
    "app\\(.*\\)", "home", "noapp", "anyapp", "android", "window\\(.*\\)",
  );

  my @lines = split /[\n\r]+/, $contents;
  for my $line(@lines){
    next if $line =~ /^\s*(#.*)?$/;
    if($line =~ /^\s*action\s*=\s*($actionRe),([^,]+),(\w+(?:\(.*\))?)$/){
      my ($action, $patternString, $condition) = ($1, $2, $3);
      my @pattern = parsePatternString($patternString);
      push @actions, {
        action => $action,
        pattern => [@pattern],
        condition => $condition,
      };
      if($condition !~ /^($condRe)$/ix){
        die "invalid condition: $condition\n";
      }
    }else{
      die "MALFORMED CONFIG LINE: $line";
    }
  }

  return @actions;
}

sub sortActionsLongestFirst(@){
  my @actions = @_;
  return sort {
    my $ptrnLenA = @{$$a{pattern}};
    my $ptrnLenB = @{$$b{pattern}};
    my $actionA = $$a{action};
    my $actionB = $$b{action};
    my $conditionA = $$a{condition};
    my $conditionB = $$b{condition};

    return 0
      || $ptrnLenB <=> $ptrnLenA
      || $actionA cmp $actionB
      || $conditionA cmp $conditionB
      ;
  } @actions;
}

sub formatPattern(@){
  return join(" ", condensePattern(@_));
}

sub condensePattern(@){
  my @pattern = @_;
  return () if @pattern == 0;
  my $event = shift @pattern;
  my $btnShortName = $BTN_SHORTNAMES{$$event{buttonId}};
  if($$event{type} eq $TYPE_PRESS){
    for(my $i=0; $i<@pattern; $i++){
      my $btnRelEvent = $pattern[$i];
      next if $$btnRelEvent{buttonId} != $$event{buttonId};
      next if $$btnRelEvent{type} ne $TYPE_RELEASE;

      my @group = @pattern[0..$i-1];
      @pattern = @pattern[$i+1..$#pattern];

      if(@group > 0){
        return ("$btnShortName(", condensePattern(@group), ")", condensePattern(@pattern));
      }else{
        return ("$btnShortName", condensePattern(@pattern));
      }
    }
    return ("$btnShortName-press");
  }elsif($$event{type} eq $TYPE_RELEASE){
    return ("$btnShortName-release", condensePattern(@pattern));
  }else{
    die "INVALID EVENT TYPE: $$event{type}\n";
  }
}

sub parsePatternString($){
  my ($patternString) = @_;

  my $btnRe = "(?:" . join("|", values %BTN_SHORTNAMES) . ")";
  my $eventRe = "(?:$btnRe(?:-press|-release)?)";

  $patternString = lc $patternString;

  if($patternString =~ /^\s*$/){
    return ();
  }elsif($patternString =~ /^\s*($eventRe)(?:\s+|$)(.*)$/){
    my ($event, $ptrn) = ($1, $2);
    if($event =~ /^$btnRe$/){
      return ("$event-press", "$event-release", parsePatternString($ptrn));
    }else{
      return ("$event", parsePatternString($ptrn));
    }
  }elsif($patternString =~ /^\s*($btnRe)\s*\((.*)$/){
    my ($btnName, $ptrn) = ($1, $2);
    my $len = length $ptrn;
    my $parensCount = 1;
    my $closeParensIndex = undef;
    for(my $i=0; $i<$len; $i++){
      my $c = substr $ptrn, $i, 1;
      if($c eq "("){
        $parensCount++;
      }elsif($c eq ")"){
        $parensCount--;
        if($parensCount == 0){
          $closeParensIndex = $i;
          last;
        }
      }
    }
    if(not defined $closeParensIndex){
      die "unmatched open-parens: $patternString\n";
    }
    my $group = substr $ptrn, 0, $closeParensIndex;
    my $suffix = substr $ptrn, $closeParensIndex+1;

    return (
      "$btnName-press",
      parsePatternString($group),
      "$btnName-release",
      parsePatternString($suffix),
    );
  }else{
    die "invalid pattern: $patternString\n";
  }
}

sub checkAction($$@){
  my ($patternMatchMode, $action, @pattern) = @_;

  my @actionPtrn = @{$$action{pattern}};
  my $actionPtrnRe = join ".*", @actionPtrn;

  my $patternStr = join " ", map {
    "$BTN_SHORTNAMES{$$_{buttonId}}-$$_{type}"
  } @pattern;

  if($patternMatchMode eq $PTRN_MATCH_ANYWHERE){
    $actionPtrnRe = "$actionPtrnRe";
  }elsif($patternMatchMode eq $PTRN_MATCH_END_ONLY){
    $actionPtrnRe = "$actionPtrnRe\$";
  }else{
    die "ERROR: unknown pattern match mode $patternMatchMode\n";
  }

  if($patternStr =~ /$actionPtrnRe/){
    return 1;
  }else{
    return 0;
  }
}

sub checkCondition($){
  my ($condition) = @_;

  if($condition =~ /^(always)$/i){
    return 1;
  }elsif($condition =~ /^(screenLocked)$/i){
    return isScreenLocked() ? 1 :0;
  }elsif($condition =~ /^(screenUnlocked)$/i){
    return isScreenLocked() ? 0 : 1;
  }elsif($condition =~ /^app\((.*)\)$/i){
    my $cmdRegex = $1;
    return isWindowActive($cmdRegex);
  }elsif($condition =~ /^(home)$/i){
    return 0 if isScreenLocked();
    return isAnyWindowActive() ? 0 : 1;
  }elsif($condition =~ /^(noapp)$/i){
    return isAnyWindowActive() ? 0 : 1;
  }elsif($condition =~ /^(anyapp)$/i){
    return isAnyWindowActive() ? 1 : 0;
  }elsif($condition =~ /^(android)$/i){
    return isWindowActive('^system_server$');
  }elsif($condition =~ /^window\((.*)\)$/i){
    my $titleRegex = $1;
    return 0 if isScreenLocked();
    return lastWindowTitleMatches($titleRegex);
  }else{
    die "ERROR: invalid condition $condition\n";
  }
}

sub getActionCommand($){
  my ($actionStr) = @_;
  if(defined $DBUS_ACTIONS{$actionStr}){
    return $DBUS_ACTIONS{$actionStr};
  }elsif($actionStr =~ /^cmd\((.*)\)$/){
    return $1;
  }elsif($actionStr =~ /^repeat\((?:\d+),(.*)\)$/){
    return $1;
  }else{
    die "invalid action: $actionStr\n";
  }
}

sub isScreenLocked(){
  my @cmd = qw(
    dbus-send
      --system
      --dest=com.nokia.mce
      --print-reply
      --type=method_call
      /com/nokia/mce/request
      com.nokia.mce.request.get_display_status
  );
  print "@cmd\n";
  open CMD, "-|", @cmd or die "could not run @cmd\n$!\n";
  my $out = join '', <CMD>;
  close CMD;

  my $status = "";
  if($out =~ /^method.*\n\s*string\s*['"](on|off)['"]\s*$/){
    $status = $1;
  }

  if($status eq "on"){
    return 0;
  }elsif($status eq "off"){
    return 1;
  }else{
    print STDERR "could not read display status with @cmd\n";
    return 0;
  }
}

sub allEq(@){
  for my $elem(@_){
    my $first = $_[0];
    if($first ne $elem){
      return 0;
    }
  }
  return 1;
}

#dbusSendCmd(DEST, OBJECT_PATH, FUNCTION, ARG, ARG ...)
#generate the following command:
#  dbus-send --print-reply --dest=DEST OBJECT_PATH FUNCTION ARG ARG ...
#replace variables in that command as follows:
#  <DEST>     => DEST
#  <DEST_OBJ> => DEST with all '.'s replace with '/'s
sub dbusSendCmd($$$@){
  my ($dest, $objectPath, $function, @args) = @_;
  my $destObj = $dest;
  $destObj =~ s/\./\//g;
  my $millis = nowMillis();
  my $cmd = "dbus-send --print-reply --dest=$dest $objectPath $function @args";
  $cmd =~ s/<DEST_OBJ>/$destObj/g;
  $cmd =~ s/<DEST>/$dest/g;
  $cmd =~ s/<MILLIS>/$millis/g;
  return $cmd;
}

sub getActiveWindowCmd(){
  my $pid = getActiveWindowPid();
  return undef if not defined $pid;
  my $cmd = `ps --no-headers -o cmd -q $pid 2>/dev/null`;
  chomp $cmd;
  if($cmd =~ /\w/ and $cmd !~ /[\r\n]/){
    return $cmd;
  }
}

sub getActiveWindowPid(){
  my @cmd = qw(dbus-send --print-reply --system
    --dest=org.nemomobile.lipstick /
    org.nemomobile.compositor.privateTopmostWindowProcessId
  );
  my $res = join '', `@cmd 2>/dev/null`;
  if($res =~ /int32 (\d+)/){
    return $1;
  }else{
    return undef;
  }
}

sub lastWindowTitleMatches($){
  my ($titleRegex) = @_;
  if(-f $LAST_WINDOW_TITLE_FILE){
    my $lastWindowTitle = `cat $LAST_WINDOW_TITLE_FILE 2>/dev/null`;
    if($lastWindowTitle =~ /$titleRegex/){
      return 1;
    }
  }
  return 0;
}

sub isWindowActive($){
  my ($cmdRegex) = @_;

  my $winCmd = getActiveWindowCmd();
  return 0 if not defined $winCmd;
  print "CHECKING: $winCmd =~ /$cmdRegex/\n" if defined $winCmd;
  return $winCmd =~ /$cmdRegex/ ? 1 : 0;
}

sub isAnyWindowActive() {
  return defined getActiveWindowPid() ? 1 : 0;
}

sub nowMillis(){
  return int(time * 1000.0 + 0.5);
}

&main(@ARGV);
